// (c) Cartesi and individual authors (see AUTHORS)
// SPDX-License-Identifier: Apache-2.0 (see LICENSE)

pragma solidity ^0.8.8;

import {IERC1155Receiver} from "@openzeppelin/contracts/token/ERC1155/IERC1155Receiver.sol";
import {IERC721Receiver} from "@openzeppelin/contracts/token/ERC721/IERC721Receiver.sol";

import {IConsensus} from "../consensus/IConsensus.sol";
import {OutputValidityProof} from "../common/OutputValidityProof.sol";

/// @notice The base layer incarnation of an application running on the execution layer.
/// @notice The state of the application advances through inputs sent to an `IInputBox` contract.
/// @notice These inputs can be sent either directly, or indirectly through portals.
/// @notice Reader nodes can retrieve inputs sent to the `IInputBox` contract through events, and feed them into the machine.
/// @notice Validator nodes can also submit claims to the `IConsensus` contract (see the `getConsensus` function).
/// @notice Once accepted, claims can be used to validate outputs generated by the machine.
/// @notice Some outputs are executable, which means they can have on-chain side effects.
/// @notice Every application is subscribed to some consensus, and may be governed by some owner.
/// The consensus has the power to accept claims, which, in turn, are used to validate outputs.
/// Meanwhile, the owner can replace the consensus at any time.
/// Therefore, the users of an application must trust both the consensus and the application owner.
/// @notice There are several ownership models to choose from:
/// - no owner (address zero)
/// - individual signer (externally-owned account)
/// - multiple signers (multi-sig)
/// - DAO (decentralized autonomous organization)
/// - self-owned application (off-chain governance logic)
/// @notice See `IConsensus` for examples of consensus models.
interface IApplication is IERC721Receiver, IERC1155Receiver {
    // Events

    /// @notice MUST trigger when a new consensus is chosen.
    /// @param newConsensus The new consensus
    event NewConsensus(IConsensus newConsensus);

    /// @notice MUST trigger when an output is executed.
    /// @param outputIndex The index of the output
    /// @param output The output
    event OutputExecuted(uint64 outputIndex, bytes output);

    // Errors

    /// @notice Could not execute an output, because the application contract doesn't know how to.
    /// @param output The output
    error OutputNotExecutable(bytes output);

    /// @notice Could not execute an output, because it was already executed.
    /// @param output The output
    error OutputNotReexecutable(bytes output);

    /// @notice Raised when the output hashes siblings array has an invalid size.
    /// @dev Please consult `CanonicalMachine` for the maximum number of outputs.
    error InvalidOutputHashesSiblingsArrayLength();

    /// @notice Raised when the required claim was not accepted by the current consensus.
    error ClaimNotAccepted(bytes32 claim);

    // Permissioned functions

    /// @notice Migrate the application to a new consensus.
    /// @param newConsensus The new consensus
    /// @dev Can only be called by the application owner.
    function migrateToConsensus(IConsensus newConsensus) external;

    // Permissionless functions

    /// @notice Execute an output.
    /// @param output The output
    /// @param proof The proof used to validate the output against
    ///              a claim submitted to the current consensus contract
    /// @dev On a successful execution, emits a `OutputExecuted` event.
    /// @dev May raise any of the errors raised by `validateOutput`,
    /// as well as `OutputNotExecutable` and `OutputNotReexecutable`.
    function executeOutput(
        bytes calldata output,
        OutputValidityProof calldata proof
    ) external;

    /// @notice Check whether an output has been executed.
    /// @param outputIndex The index of output
    /// @return Whether the output has been executed before
    function wasOutputExecuted(
        uint256 outputIndex
    ) external view returns (bool);

    /// @notice Validate an output.
    /// @param output The output
    /// @param proof The proof used to validate the output against
    ///              a claim submitted to the current consensus contract
    /// @dev May raise any of the errors raised by `validateOutputHash`.
    function validateOutput(
        bytes calldata output,
        OutputValidityProof calldata proof
    ) external view;

    /// @notice Validate an output hash.
    /// @param outputHash The output hash
    /// @param proof The proof used to validate the output against
    ///              a claim submitted to the current consensus contract
    /// @dev May raise `InvalidOutputHashesSiblingsArrayLength`
    /// or `ClaimNotAccepted`.
    function validateOutputHash(
        bytes32 outputHash,
        OutputValidityProof calldata proof
    ) external view;

    /// @notice Get the application's template hash.
    /// @return The application's template hash
    function getTemplateHash() external view returns (bytes32);

    /// @notice Get the current consensus.
    /// @return The current consensus
    function getConsensus() external view returns (IConsensus);
}
